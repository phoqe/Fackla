plugins {
    id "com.android.application"
    id "kotlin-android"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
    id "com.google.firebase.firebase-perf"
}

def credFile = rootProject.file("cred.properties")
def credConfig = new Properties()

credConfig.load(new FileInputStream(credFile))

android {
    signingConfigs {
        release {
            storeFile file(credConfig["STORE_FILE"])
            storePassword credConfig["STORE_PASS"]
            keyAlias credConfig["KEY_ALIAS"]
            keyPassword credConfig["KEY_PASS"]
        }
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.phoqe.fackla"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 10
        versionName "1.4.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            debugSymbolLevel "FULL"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " (Debug)"
            minifyEnabled false
            pseudoLocalesEnabled true
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            shrinkResources true
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:1.5.0"
    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "com.google.android.material:material:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "com.android.support:multidex:1.0.3"
    implementation "androidx.fragment:fragment-ktx:1.3.4"
    implementation "androidx.activity:activity-ktx:1.2.3"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    // Mapbox
    implementation "com.mapbox.mapboxsdk:mapbox-android-sdk:9.6.1"
    implementation "com.mapbox.mapboxsdk:mapbox-android-plugin-localization-v9:0.12.0"

    // EventBus
    implementation "org.greenrobot:eventbus:3.2.0"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:27.1.0")
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-perf-ktx"
    implementation "com.google.firebase:firebase-appcheck-safetynet:16.0.0-beta01"

    // LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.7"

    // Material Foundation
    implementation project(path: ":material_foundation")

    // Testing
    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.test:core:1.3.0"
    testImplementation "org.mockito:mockito-core:2.12.0"

    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.test:rules:1.3.0"
    androidTestImplementation "androidx.test:runner:1.3.0"
}